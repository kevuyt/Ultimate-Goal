configurations {doc {transitive false}}

buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {classpath 'com.android.tools.build:gradle:4.0.2'}
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

repositories {
    mavenCentral()

    flatDir {dirs '../libs'}
}

dependencies {
    doc 'org.firstinspires.ftc:Hardware:6.2.1'
    doc 'org.firstinspires.ftc:RobotCore:6.2.1'
    doc 'org.firstinspires.ftc:FtcCommon:6.2.1'
    doc 'org.firstinspires.ftc:OnBotJava:6.2.1'
    doc 'org.firstinspires.ftc:Inspection:6.2.1'
}

task extractJavadoc {
    doLast {
        def componentIds = configurations.doc.incoming.resolutionResult.allDependencies.collect { it.selected.id }
        def result = dependencies.createArtifactResolutionQuery()
                .forComponents(componentIds)
                .withArtifacts(JvmLibrary, SourcesArtifact, JavadocArtifact)
                .execute()
        for (component in result.resolvedComponents) {
            component.getArtifacts(JavadocArtifact).each {artifact ->
                def version = artifact.identifier.componentIdentifier.version
                def libName = artifact.identifier.componentIdentifier.moduleIdentifier.name
                copy {
                    from zipTree(artifact.file)
                    into "docs/$version/$libName/"
                }
            }
        }
    }
}